:local queueName "$leaseActMAC";

:if ($leaseBound = "1") do={
    # Define default speed limits

    
    # Retrieve hostname
    :local hostName [/ip dhcp-server lease get [find where active-mac-address=$leaseActMAC && active-address=$leaseActIP] host-name];

    # Retrieve device port
    :local devicePort "Unknown";
    /interface bridge host
    :local searchResult [find where mac-address=$leaseActMAC];
    :if ([:len $searchResult] > 0) do={
        :set devicePort [get $searchResult on-interface];
    } else={
        /ip arp
        :set searchResult [find where mac-address=$leaseActMAC];
        :if ([:len $searchResult] > 0) do={
            :set devicePort [get $searchResult interface];
        }
    }

    # Match the device IP with firewall address lists
    :local addressList "None";
    :foreach list in=[/ip firewall address-list find where address=$leaseActIP] do={
        :set addressList [/ip firewall address-list get $list list];
    }

    # Construct the comment in the required format
    :local queueName ("[" . $addressList . "] " . $devicePort . " : " . $hostName);
    :local queueComment "$leaseActMAC";
    :local speedLimit "10M/10M";
    :local comment "Rouge";

    # Flag to check if the IP was found in an address list
    :local foundInList false;

    # Check if the IP address belongs to any address list
    :foreach list in=[/ip firewall address-list find where address=$leaseActIP] do={
        :local addressList [/ip firewall address-list get $list list];

        # Match against specific address lists dynamically
        :if ($addressList != "") do={
            :set foundInList true;

            # Assign speed and comment based on the address list
            :set comment $addressList;
            :if ($addressList = "Family") do={ :set speedLimit "20M/20M"; }
            :if ($addressList = "Servers") do={ :set speedLimit "100M/100M"; }
            :if ($addressList = "CCTV") do={ :set speedLimit "50M/50M"; }
            :if ($addressList = "IOT") do={ :set speedLimit "50M/50M"; }
            :if ($addressList = "AP") do={ :set speedLimit "50M/50M"; }
            :if ($addressList = "ALEXA") do={ :set speedLimit "100M/100M"; }
            :if ($addressList = "Blocklist") do={ :set speedLimit "56K/56K"; }
            :if ($addressList = "Fileserver") do={ :set speedLimit "500M/500M"; }
            
            # Update comment with queueName in the firewall address list entry
            /ip firewall address-list set $list comment=$queueName;
        }
    }
    # Add or update the queue
    :if ([:len [/queue simple find name=$queueName]] > 0) do={
        /queue simple set [find name=$queueName] max-limit=$speedLimit comment=$queueComment;
    } else={
        /queue simple add name=$queueName target=($leaseActIP . "/32") max-limit=$speedLimit comment=$queueComment;
    }
} else={
    # Lease is unbound, check for the queue with the MAC address in the comment
    :foreach queue in [/queue simple find] do={
        :local currentComment [/queue simple get $queue comment];
        :if ($currentComment = "$leaseActMAC") do={
            :log info ("Removing queue with comment MAC: " . $leaseActMAC);
            /queue simple remove $queue;
        }
    }
}
